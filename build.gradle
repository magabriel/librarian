group 'librarian'
version '0.7'

apply plugin: 'java'
apply plugin: 'kotlin'

apply plugin: 'kotlin-kapt'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.yaml:snakeyaml:1.17'
    compile 'com.rometools:rome:1.7.0'
    testCompile('org.junit.jupiter:junit-jupiter-api:5.0.2')
    testRuntime('org.junit.jupiter:junit-jupiter-engine:5.0.2')

    // to avoid a warning introduced by ROME
    compile group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.21'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
}

jar {
    manifest {
        attributes  'Main-Class': 'com.mags.librarian.Main',
                    'Implementation-Title': 'Librarian',
                    'Implementation-Version': version,
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'),
                    'Built-Gradle': gradle.gradleVersion
    }

    // include all dependencies in jar
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

}
buildscript {
    ext.kotlin_version = '1.2.51'
    ext.dagger_version = '2.10'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
// At the moment for non-Android projects you need to explicitly
// mark the code generated by kapt as 'generated source code'
// for correct highlighting and resolve in IDE.
idea {
    module {
        sourceDirs += file('build/generated/source/kapt/main')
        generatedSourceDirs += file('build/generated/source/kapt/main')
    }
}